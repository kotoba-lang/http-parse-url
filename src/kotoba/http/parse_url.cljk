(ns kotoba.http.parse-url
  "parse-url -- addressed on its own.

  Split out of kotoba.lang.http on 2026-09-09 (ADR-2609091200). The unit
  here is the DEFINITION, and this repo's deps.edn names exactly the
  definitions it reaches -- nothing else.
"
  )

(defn parse-url
  "Parse a URL string into `{:scheme :host :port :path :query}`. Minimal:
  handles `scheme://host[:port]/path?query`. Path/query may be nil."
  [s]
  (let [s (str s)]
    (if-let [m (re-matches #"(?s)^([^:/?#]+)://([^:/?#]+)(?::(\d+))?(/[^?#]*)?(\?[^#]*)?(#.*)?$" s)]
      (let [[_ scheme host port path query] m]
        (cond-> {:scheme scheme :host host}
          (some? port)  (assoc :port (try #?(:clj (Integer/parseInt port) :cljs (js/parseInt port 10))
                                          (catch #?(:clj Throwable :cljs :default) _ nil)))
          (some? path)  (assoc :path path)
          (some? query) (assoc :query (subs query 1))))   ; strip leading '?'
      (throw (ex-info "http/parse-url: malformed url" {:input s})))))
